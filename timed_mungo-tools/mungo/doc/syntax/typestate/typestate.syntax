Keywords
---------

TYPESTATE			::=	"typestate"
IDENTIFIER			::=	java identifiers
VOID				::=	"void"
END				::=	"end"

Usual keyword single symbol operators

LBRACE
RBRACE
LPAREN
RPAREN
LT
GT
EQ
COMMA
COLON

BNF for the declaration of typestate protocols
------------------------------------------------

type_declaration		::=	typestate_declaration
				;

typestate_declaration		::=	TYPESTATE IDENTIFIER LBRACE IDENTIFIER EQ typestate state_name_list RBRACE
				|	TYPESTATE IDENTIFIER LBRACE IDENTIFIER EQ typestate RBRACE
				;

state_name_list			::=	state_name
				|	state_name_list state_name
				;

state_name			::=	IDENTIFIER EQ typestate
				;

typestate_list			::=	method
				|	typestate_list COMMA method
				;

typestate			::=	LBRACE typestate_list RBRACE
				|	LBRACE RBRACE
				|	END
				|	t_label
				;

method				::=	ttype IDENTIFIER LPAREN ttype_list RPAREN COLON typestate
				|	ttype IDENTIFIER LPAREN RPAREN COLON typestate
				|	ttype IDENTIFIER LPAREN ttype_list RPAREN COLON t_switch
				|	ttype IDENTIFIER LPAREN RPAREN COLON t_switch
				;

t_switch			::=	LT switch_case_list GT
				;

switch_case_list		::=	switch_case
				|	switch_case_list COMMA switch_case
				;

switch_case			::=	IDENTIFIER COLON typestate
				;


t_label				::=	IDENTIFIER
				;

ttype				::=	type LT typestate GT
				|	type
				|	VOID
				;

ttype_list			::=	ttype
				|	ttype_list COMMA ttype
				;


